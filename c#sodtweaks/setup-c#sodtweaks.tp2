
BACKUP ~c#sodtweaks/backup~
AUTHOR ~https://www.gibberlings3.net/forums/topic/30427-jasteys-sod-tweakpack/~ 


VERSION ~v9~


README ~c#sodtweaks/readme.c#sodtweaks.%LANGUAGE%.txt~ ~c#sodtweaks/readme.c#sodtweaks.english.txt~


AUTO_TRA ~c#sodtweaks/translations/autotra/%s~

ALWAYS

/* read in general definitions */
ACTION_IF GAME_IS ~bgee~ THEN BEGIN
  INCLUDE ~c#sodtweaks/lib/g3_bgee_cpmvars.tpa~
END

ACTION_IF GAME_IS ~eet~ THEN BEGIN
  INCLUDE ~EET/other/cpmvars/eet_cpmvars.tpa~
END

/* from subtledoctor */
 DEFINE_ACTION_FUNCTION starting_xp INT_VAR value = 64000 BEGIN
	COPY_EXISTING ~sodstrta.2da~ ~override~
	  COUNT_2DA_ROWS 2 rows
	  FOR (row = 1; row < rows; ++row) BEGIN
		READ_2DA_ENTRY row 0 2 setting
		PATCH_IF (~%setting%~ STRING_EQUAL_CASE ~START_XP~) BEGIN
		  SET_2DA_ENTRY row 1 2 %value%
		END
	  END
	IF_EXISTS BUT_ONLY
  END

/* check for a ready SoD */

ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN
  FAIL @1002 /* ~Modmerge or Argent's DLC Merger is required before mods can be installed on this game.~ */
END

  INCLUDE ~c#sodtweaks/lib/extra_regexp_vars.tph~
  OUTER_SPRINT newline ~%WNL%%LNL%%MNL%%TAB% ~

  OUTER_SET eet_200000 = GAME_IS ~eet~ ? 200000 : 0

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
  OUTER_SPRINT ~eet_2~ ~~
END
ACTION_IF GAME_IS ~eet~ THEN BEGIN
  OUTER_SPRINT ~eet_2~ ~2~
END



//////////////////////////////////////////////////////////////////////
/* all following actions are only processed ONCE for the whole mod, independent of un- and reinstalling of single components */

ACTION_IF !FILE_EXISTS ~c#sodtweaks/install/c#sodtweaksinstall.mrk~ BEGIN

    /* Copies tra files into the autotra-folder (to leave the originals untouched) */
    DEFINE_ACTION_FUNCTION autotra_workaround BEGIN
      COPY ~c#sodtweaks/translations/english~  ~c#sodtweaks/translations/autotra/%LANGUAGE%~
      COPY ~c#sodtweaks/translations/%LANGUAGE%~  ~c#sodtweaks/translations/autotra/%LANGUAGE%~
    END

    LAF autotra_workaround END

  ACTION_DEFINE_ARRAY fl#noconvert BEGIN setup END

  ACTION_DEFINE_ARRAY fl#reload BEGIN game END

    LAF HANDLE_CHARSETS
      INT_VAR
        infer_charsets = 1
      STR_VAR
        tra_path = EVAL ~c#sodtweaks/translations/autotra~
        noconvert_array = fl#noconvert
        reload_array = fl#reload
    END


//CamDawgs CD_STATE_NOTVALID 
    APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~



  COPY ~c#sodtweaks/install/component.xx~ ~c#sodtweaks/install/c#sodtweaksinstall.mrk~

  END // c#sodtweaksinstall.mrk

 //reloading of tras:
  LOAD_TRA ~c#sodtweaks/translations/autotra/english/game.tra~
  LOAD_TRA ~c#sodtweaks/translations/autotra/%LANGUAGE%/game.tra~

END //ALWAYS


LANGUAGE ~English~
         ~english~   
         ~c#sodtweaks/translations/english/setup.tra~
	 ~c#sodtweaks/translations/english/game.tra~

LANGUAGE ~Deutsch~
         ~german~   
         ~c#sodtweaks/translations/german/setup.tra~
	 ~c#sodtweaks/translations/german/game.tra~

LANGUAGE ~Russian~
         ~russian~   
         ~c#sodtweaks/translations/english/setup.tra~
	 ~c#sodtweaks/translations/russian/game.tra~

LANGUAGE ~French~
         ~french~   
         ~c#sodtweaks/translations/french/setup.tra~
	 ~c#sodtweaks/translations/french/game.tra~


/* ~Jastey's SoD Tweaks for SoD and EET~ */



////////////////////////////////////////////////////////////////////////
//////
////// 1 ~Ending Independent on PC Replies~
//////
////////////////////////////////////////////////////////////////////////


BEGIN @1003 /* ~Ending Independent on PC Replies~ */ DESIGNATED 1
LABEL ~JasteySodTweaks-EndingIndependentReplies~
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~c#anotherfinehell.tp2~ ~0~ @1024 /* ~Another mod is making this component unneeded.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @1017
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @1017


/*
from BDBELT.dlg
points for good:
Global("bd_mdd420_morale","global",1): priestess of Bhaal released to do good
Global("bd_MDD1001_morale","global",1): did not poison the crusaders supplies
Global("bd_sdd302_uncommon_cold","global",3): searched for cure for soldiers at the coalition camp
Global("bd_thrix_sacrifice_self","global",1): offered own soul to Thrix
Class(Player1,PALADIN_ALL) !Kit(Player1,Blackguard): paladin not fallen
ReputationGT(Player1,16): high rep


points for evil:
Global("bd_mdd420_morale","global",-1): told priestess of Bhaal to murder
Global("bd_MDD1001_morale","global",-1): did poison the crusaders supplies
Global("SDD303_executed","global",1): executed traitor at coalition camp
Global("bd_thrix_sacrifice_companion","global",1): sold companion's soul to Thrix
Class(Player1,PALADIN_ALL) Kit(Player1,Blackguard): is BLackguard
ReputationLT(Player1,6): low rep

*/


/* in bdcut61t.bcs */
/* count good points up: */

<<<<<<<< ...inlined/c#sodtweaks_trialend.baf
IF
	Global("bd_mdd420_morale","global",1)
THEN
	RESPONSE #100
		IncrementGlobal("c#st_bd_trial_hero","bd0035",1)
END

IF
	Global("bd_MDD1001_morale","global",1)
THEN
	RESPONSE #100
		IncrementGlobal("c#st_bd_trial_hero","bd0035",1)
END

IF
	Global("bd_sdd302_uncommon_cold","global",3)
THEN
	RESPONSE #100
		IncrementGlobal("c#st_bd_trial_hero","bd0035",1)
END

IF
	Global("bd_thrix_sacrifice_self","global",1)
THEN
	RESPONSE #100
		IncrementGlobal("c#st_bd_trial_hero","bd0035",1)
END

IF
	Class(Player1,PALADIN_ALL) !Kit(Player1,Blackguard)
THEN
	RESPONSE #100
		IncrementGlobal("c#st_bd_trial_hero","bd0035",1)
END

IF
	ReputationGT(Player1,16)
THEN
	RESPONSE #100
		IncrementGlobal("c#st_bd_trial_hero","bd0035",1)
END


/* count evil points up: */
IF
	Global("bd_mdd420_morale","global",-1)
	THEN
		RESPONSE #100
			IncrementGlobal("c#st_bd_trial_evil","bd0035",1)
END
IF
	Global("bd_MDD1001_morale","global",-1)
	THEN
		RESPONSE #100
			IncrementGlobal("c#st_bd_trial_evil","bd0035",1)
END

IF
	Global("SDD303_executed","global",1)
	THEN
		RESPONSE #100
			IncrementGlobal("c#st_bd_trial_evil","bd0035",1)
END

IF
	Global("bd_thrix_sacrifice_companion","global",1)
	THEN
		RESPONSE #100
			IncrementGlobal("c#st_bd_trial_evil","bd0035",1)
END

IF
	Class(Player1,PALADIN_ALL) Kit(Player1,Blackguard)
	THEN
		RESPONSE #100
			IncrementGlobal("c#st_bd_trial_evil","bd0035",1)
END

IF
	ReputationLT(Player1,6)
	THEN
		RESPONSE #100
			IncrementGlobal("c#st_bd_trial_evil","bd0035",1)
END

/* the variables are set here, regardless of how much the PC tells at the trial */
IF
	OR(2)
		!GlobalLT("c#st_bd_trial_hero","bd0035",3)
		!GlobalLT("c#st_bd_trial_evil","bd0035",3)

	THEN
		RESPONSE #100
			SetGlobal("bd_trial_Innocent","GLOBAL",1)
			SetGlobal("bd_player_exiled","global",1)
END

>>>>>>>>
EXTEND_TOP ~bdcut61t.bcs~ ~...inlined/c#sodtweaks_trialend.baf~ EVALUATE_BUFFER


////////////////////////////////////////////////////////////////////////
//////
////// 2A ~Do Not Miss NPC-PC Dialogues: For NPCs in Party Only~
//////
////////////////////////////////////////////////////////////////////////


BEGIN @1005 /* ~For NPCs in Party Only~ */ DESIGNATED 2
LABEL ~JasteySodTweaks-DoNotMissDialogues_NPCInParty~
SUBCOMPONENT @1004 /* ~Do Not Miss NPC-PC Dialogues~ */
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @1017
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @1017

INCLUDE ~c#sodtweaks/MissedDialogues/lib/activate_partynpc.tpa~





////////////////////////////////////////////////////////////////////////
//////
////// 2B ~Do Not Miss NPC-PC Dialogues: Activate All Dialogues Always~
//////
////////////////////////////////////////////////////////////////////////

BEGIN @1006 /* ~Activate All NPC-PC Dialogues Always~ */ DESIGNATED 3
LABEL ~JasteySodTweaks-DoNotMissDialogues_All~
SUBCOMPONENT @1004 /* ~Do Not Miss NPC-PC Dialogues~ */
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @1017
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @1017

INCLUDE ~c#sodtweaks/MissedDialogues/lib/activate_partynpc.tpa~


/* bdport01.bcs - pep lovetalk before abyss - not considered */

EXTEND_BOTTOM ~bdrom01.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom01.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom02.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom02.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom03.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom03.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom04.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom04.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom05.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom05.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom06.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom06.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom07.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom07.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom08.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom08.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom09.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom09.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom10.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom10.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom11.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom11.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom12.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom12.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom13.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom13.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom14.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom14.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom15.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom15.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom16.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom16.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom17.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom17.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrom18.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrom18.baf~ EVALUATE_BUFFER


/* Add Display String Head info for activated dialogues */

EXTEND_BOTTOM ~bdcorwin.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdcorwin_add.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bddorn.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bddorn_add.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdglint.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdglint_add.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdneera.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdneera_add.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdrasaad.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdrasaad_add.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdsafana.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdsafana_add.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdviconi.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdviconi_add.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdvoghil.bcs~ ~c#sodtweaks/MissedDialogues/c#st_bdvoghil_add.baf~ EVALUATE_BUFFER



////////////////////////////////////////////////////////////////////////
//////
////// 3 ~Skip Korlasz's Dungeon~
//////
////////////////////////////////////////////////////////////////////////


BEGIN @1007 /* ~Skip Korlasz's Dungeon~ */ DESIGNATED 4
LABEL ~JasteySodTweaks-SkipKorlaszDungeon~
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @1017
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @1017

/* patching of item "gold" by Argent with many thanks */
OUTER_PATCH ~~ BEGIN
  SET slot = "-1"
  FOR (bit = 14; bit < 32; ++bit) BEGIN // skipping first 14 hardcoded bits
    SET value = 1 << bit
    LOOKUP_IDS_SYMBOL_OF_INT symbol ~itemflag~ value
    PATCH_IF (~%symbol%~ STR_EQ ~%value%~) BEGIN
      SET slot = value
      SET bit = 32
    END
  END
END

ACTION_IF (slot > 0) BEGIN
  APPEND ~itemflag.ids~ ~%slot% GOLD~
  COPY_EXISTING ~misc07.itm~ ~override~
    WRITE_LONG 0x18 (THIS | slot)
END ELSE BEGIN
  // Instead of a warning you could reuse a rarely used reserved item flag, e.g. ADAMANTINE.
  WARN ~Could not find free item flag. (This is not game-breaking. Carry on.)~
END

/* taken and adapted from Argent's "Skip Chateau Irenicus" with many thanks */
  INCLUDE ~%MOD_FOLDER%/SkipDungeon/lib/tables.tph~

// Creating container in Duchal Palce 3rd level for dungeon items
COPY_EXISTING ~bd0103.ARE~ ~override~
  LPF fj_are_structure
  INT_VAR
    fj_loc_x          = 175
    fj_loc_y          = 430
    fj_type           = 3
    fj_lock_diff      = 0
//    fj_flags          = (1 << 5)  // initially disabled
    fj_trap_remove_diff = 0
    fj_box_left       = 132
    fj_box_top        = 388
    fj_box_right      = 160
    fj_box_bottom     = 407
    fj_vertex_0       = 132 + (400 << 16)
    fj_vertex_1       = 151 + (388 << 16)
    fj_vertex_2       = 160 + (395 << 16)
    fj_vertex_3       = 144 + (407 << 16)
//    fj_vertex_4       = 132 + (400 << 16)
  STR_VAR
    fj_structure_type = container
    fj_name           = "c#st0001"
  END
BUT_ONLY

/* Arkanis will offer to do the work */
COPY_EXISTING ~ARKANI3.cre~ ~override/c#st0001.cre~
WRITE_EVALUATED_ASCII 0x280 ~c#sthelp~ #8   // Death variable
WRITE_EVALUATED_ASCII 0x2cc ~c#sthelp~ #8   // dialogue
WRITE_EVALUATED_ASCII 0x248 ~~ #8   // Override script
WRITE_EVALUATED_ASCII 0x268 ~initmain~ #8   // Default script

/* if Arkanis is dead, Biff the Understudy will take over */
COPY_EXISTING ~DIALOGMO.cre~ ~override/c#st0002.cre~
WRITE_EVALUATED_ASCII 0x280 ~c#sthelp~ #8   // Death variable
WRITE_EVALUATED_ASCII 0x2cc ~c#sthelp~ #8   // dialogue
WRITE_EVALUATED_ASCII 0x248 ~shout~ #8   // Override script
WRITE_EVALUATED_ASCII 0x250 ~initmain~ #8   // Class script
WRITE_EVALUATED_ASCII 0x258 ~seeenemy~ #8   // Race script
WRITE_EVALUATED_ASCII 0x260 ~wtarsgt~ #8   // General script

EXTEND_BOTTOM ~bd0120.bcs~ ~c#sodtweaks/SkipDungeon/bd0120.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bd0130.bcs~ ~c#sodtweaks/SkipDungeon/bd0130.baf~ EVALUATE_BUFFER

EXTEND_BOTTOM ~bd0103.bcs~ ~c#sodtweaks/SkipDungeon/bd0103_1.baf~ EVALUATE_BUFFER
 
LAF CREATE_TRANSFER_DATA
STR_VAR
  dstContainer  = ~c#st0001~
  pc_chest      = ~PlayerChest00~
  target_area   = ~bd0103~
  target_area_script   = ~bd0103~
RET
  item_destroy_party 
END
EXTEND_BOTTOM ~bd0103.bcs~ ~c#sodtweaks/SkipDungeon/bd0103_2.baf~ EVALUATE_BUFFER

COMPILE EVALUATE_BUFFER ~c#sodtweaks/SkipDungeon/c#sthel1.baf~

COMPILE EVALUATE_BUFFER ~c#sodtweaks/SkipDungeon/c#sthelp.d~


/* Crossmod with Another fine Hell - copy contents of bedside table into custom PC's chamber and back into bd0103 */
ACTION_IF ((MOD_IS_INSTALLED ~c#anotherfinehell.tp2~ ~0~) AND (MOD_IS_INSTALLED ~c#anotherfinehell.tp2~ ~2~)) THEN BEGIN
PRINT @1027 /* ~Patching item transfer into custom PC's palace residence from Another fine Hell mod.~ */

COPY_EXISTING ~c#afh3.ARE~ ~override~
  LPF fj_are_structure
  INT_VAR
    fj_loc_x          = 175
    fj_loc_y          = 430
    fj_type           = 3
    fj_lock_diff      = 0
//    fj_flags          = (1 << 5)  // initially disabled
    fj_trap_remove_diff = 0
    fj_box_left       = 132
    fj_box_top        = 388
    fj_box_right      = 160
    fj_box_bottom     = 407
    fj_vertex_0       = 132 + (400 << 16)
    fj_vertex_1       = 151 + (388 << 16)
    fj_vertex_2       = 160 + (395 << 16)
    fj_vertex_3       = 144 + (407 << 16)
//    fj_vertex_4       = 132 + (400 << 16)
  STR_VAR
    fj_structure_type = container
    fj_name           = "c#st0001"
  END
BUT_ONLY

EXTEND_BOTTOM ~c#afh3.bcs~ ~c#sodtweaks/SkipDungeon/crossmod_anotherfinehell_c#afh3.baf~ 
EXTEND_BOTTOM ~bd0103.bcs~ ~c#sodtweaks/SkipDungeon/crossmod_anotherfinehell_bd0103.baf~ EVALUATE_BUFFER

END //Another fine Hell

/* Crossmod with EndlessBG1: prevent script from opening the travel trigger if "Korlasz Dungeon is in BG1" is installed - Tomb is now cleared and sealed. */

ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~14~) THEN BEGIN
PRINT @1030 /* ~Patching travel triggers for entrance to Korlasz' Crypt from EndlessBG1.~ */
/* deactivate travel trigger into Korlasz' Dungeon if it was cleared by Arkanis/Biff and crew */

/* BG1 part: Central BG area AR0700 */
/* prevent script from opening the travel trigger (to be safe) */

COPY_EXISTING ~%CentralBaldursGate%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(GlobalLT("C#EBG1_ChangeTriggerKD","MYAREA",2)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 Global("C#st_ShortcutEnabled", "GLOBAL", 0)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/* close travel trigger */
<<<<<<<< .../ar0700_kd_done.baf
/* deactivate travel trigger */
IF
	Global("C#st_ShortcutEnabled", "GLOBAL", 1)
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",0)
	Global("C#st_CloseKD","MYAREA",0)
THEN
	RESPONSE #100
		TriggerActivation("C#EBG1_TranBD0120",FALSE)
		TriggerActivation("c#ebg1korlasz",TRUE)
		SetGlobal("C#st_CloseKD","MYAREA",1)
END
>>>>>>>>
EXTEND_BOTTOM ~%CentralBaldursGate%.bcs~ ~.../ar0700_kd_done.baf~ EVALUATE_BUFFER 

/* BG1 part: Docks area AR1200 */
/* prevent script from opening the travel trigger (to be safe) */

COPY_EXISTING ~%BaldursGateDocks%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(GlobalLT("C#EBG1_ChangeTriggerKD","MYAREA",2)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 Global("C#st_ShortcutEnabled", "GLOBAL", 0)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/* close travel trigger */
EXTEND_BOTTOM ~%BaldursGateDocks%.bcs~ ~.../ar0700_kd_done.baf~ EVALUATE_BUFFER 

/* SoD part: Ironthrone HQ area bd0050 */
/* close travel trigger */
<<<<<<<< .../bd0050_kd_done.baf
/* deactivate travel trigger */
IF
	Global("C#st_ShortcutEnabled", "GLOBAL", 1)
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",0)
	Global("C#st_CloseKD","MYAREA",0)
THEN
	RESPONSE #100
		TriggerActivation("C#EBG1_TranBD0120",FALSE)
		TriggerActivation("c#ebg1korlasz",TRUE)
		SetGlobal("C#st_CloseKD","MYAREA",1)
END
>>>>>>>>
EXTEND_BOTTOM ~bd0050.bcs~ ~.../bd0050_kd_done.baf~ EVALUATE_BUFFER

END

/* Crossmod with EndlessBG1: place items into PC's bedside table if Korlasz Dungeon is in BG1 and Palace Residence are installed */

ACTION_IF ((MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~1~) AND (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~14~)) THEN BEGIN
PRINT @1028 /* ~Patching item transfer from Korlasz Dungeon into PC's palace residence from EndlessBG1.~ */

EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L3%.bcs~ ~c#sodtweaks/SkipDungeon/crossmod_ebg1_ar0110_1.baf~ EVALUATE_BUFFER

LAF CREATE_TRANSFER_DATA
STR_VAR
  dstContainer  = ~Container3~
  pc_chest      = ~Container1~
  target_area   = EVAL ~%NBaldursGate_DucalPalace_L3%~
  target_area_script   = EVAL ~%NBaldursGate_DucalPalace_L3%~
END

EXTEND_BOTTOM ~bd0103.bcs~ ~c#sodtweaks/SkipDungeon/crossmod_ebg1_bd0103.baf~ EVALUATE_BUFFER

END //EndlessBG1

/* Transitions */
ACTION_IF (MOD_IS_INSTALLED ~transitions.tp2~ ~0~) THEN BEGIN
PRINT @1029 /* ~Patching item transfer from Korlasz Dungeon into PC's palace residence from Transitions.~ */

COPY_EXISTING ~#LBD0103.ARE~ ~override~
  LPF fj_are_structure
  INT_VAR
    fj_loc_x          = 175
    fj_loc_y          = 430
    fj_type           = 3
    fj_lock_diff      = 0
//    fj_flags          = (1 << 5)  // initially disabled
    fj_trap_remove_diff = 0
    fj_box_left       = 132
    fj_box_top        = 388
    fj_box_right      = 160
    fj_box_bottom     = 407
    fj_vertex_0       = 132 + (400 << 16)
    fj_vertex_1       = 151 + (388 << 16)
    fj_vertex_2       = 160 + (395 << 16)
    fj_vertex_3       = 144 + (407 << 16)
//    fj_vertex_4       = 132 + (400 << 16)
  STR_VAR
    fj_structure_type = container
    fj_name           = "c#st0001"
  END
BUT_ONLY

EXTEND_BOTTOM ~#LPCROOM.bcs~ ~c#sodtweaks/SkipDungeon/crossmod_transitions_#LBD0103_1.baf~ EVALUATE_BUFFER

LAF CREATE_TRANSFER_DATA
STR_VAR
  dstContainer  = ~c#st0001~
  pc_chest      = ~PlayerChest00~
  target_area   = ~#LBD0103~
  target_area_script   = ~#LPCROOM~
END

EXTEND_BOTTOM ~bd0103.bcs~ ~c#sodtweaks/SkipDungeon/crossmod_transitions_bd0103.baf~ EVALUATE_BUFFER

END //Transitions


////////////////////////////////////////////////////////////////////////
//////
////// 4 ~More Dialogue Choices & Prevent Dead Ends~
//////
////////////////////////////////////////////////////////////////////////


BEGIN @1008 /* ~More Dialogue Choices & Prevent Dead Ends~ */ DESIGNATED 5
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @1017
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @1017

COMPILE EVALUATE_BUFFER ~c#sodtweaks/MoreDialogueChoices/dialogue_choices.d~


////////////////////////////////////////////////////////////////////////
//////
////// 5 ~Use Imoen's SoD Portrait for BGII in EET&BGT~
//////
////////////////////////////////////////////////////////////////////////

BEGIN @1009 /* ~Use Imoen's SoD Portrait for BGII in EET & BGT~ */ DESIGNATED 6
LABEL ~JasteySodTweaks-ImoensSoDPortrait~
REQUIRE_PREDICATE (GAME_IS ~eet bgt~) @1001  /* ~This component is only compatible with BGT and EET.~ */

/* EET&BGT: Imoen in ID has script IMOEN.bcs */

ACTION_IF GAME_IS ~bgt~ THEN BEGIN
  COPY ~c#sodtweaks/ImoenSoDPortrait/imoen_1_60.bmp~ ~override/c#stimos.bmp~
  COPY ~c#sodtweaks/ImoenSoDPortrait/imoen_1_170.bmp~ ~override/c#stimom.bmp~

/* will just replace the ToB endportrait */
  COPY ~c#sodtweaks/ImoenSoDPortrait/imoen_1_330.bmp~ ~override/nimoenl.bmp~

  COPY ~c#sodtweaks/ImoenSoDPortrait/c#stimoe_bgt.spl~ ~override/c#stimoe.spl~
END

ACTION_IF GAME_IS ~eet~ THEN BEGIN
//  COPY_EXISTING ~bdimoens.bmp~ ~override/c#stimos.bmp~
  COPY_EXISTING ~bdimoenm.bmp~ ~override/c#stimom.bmp~
  COPY_EXISTING ~bdimoenl.bmp~ ~override/c#stimol.bmp~

  COPY ~c#sodtweaks/ImoenSoDPortrait/c#stimoe_eet.spl~ ~override/c#stimoe.spl~
END


<<<<<<<< ...inlined/imoen_portrait.baf
IF
	Global("C#st_ImoenPortraitChange","MYAREA",0)
	AreaCheck("AR0602")
THEN
	RESPONSE #100
		SetGlobal("C#st_ImoenPortraitChange","MYAREA",1)
		ApplySpellRES("c#stimoe",Myself)
		Continue()
END
>>>>>>>>

EXTEND_TOP ~IMOEN.bcs~ ~...inlined/imoen_portrait.baf~


////////////////////////////////////////////////////////////////////////
//////
////// 6 ~Restore scripted Abduction Scene Fight~
//////
////////////////////////////////////////////////////////////////////////
BEGIN @1011 /* ~Restore scripted Abduction Scene Fight~ */ DESIGNATED 7
LABEL ~JasteySodTweaks-RestoreAbductionFight~
REQUIRE_PREDICATE (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~) @1010  /* ~This component is only compatible with SoD.~ */

//BD6100.BCS
COPY_EXISTING ~BD6100.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~IncrementGlobal("bd_finale","bd6100",10)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~IncrementGlobal("bd_finale","bd6100",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~StartMovie("sodcin05")[%newline%]*EndCutSceneMode()[%newline%]*ContinueGame(FALSE)[%newline%]*EndCredits()~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~StartMovie("sodcin05") EndCutSceneMode()~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

<<<<<<<< .../BD6100-et.baf

IF
	GlobalGT("bd_finale","bd6100",0)
	InMyArea("IMOEN")
	HasItem("MINHP1","IMOEN")
THEN
	RESPONSE #100
		ActionOverride("IMOEN",DestroyItem("MINHP1"))
END

IF
  Global("C#StoryMode","BD6100",0)
  StoryModeOn()
  Global("OHSMODE","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("C#StoryMode","BD6100",1)
    SetGlobal("OHSMODE","GLOBAL",-1)
    ReallyForceSpellDeadRES("OHSMODE2",Player1)
    Continue()
END
>>>>>>>>

EXTEND_TOP ~BD6100.BCS~ ~.../BD6100-et.baf~


////////////////////////////////////////////////////////////////////////
//////
////// 7 ~Adjust PC's Starting XP at Beginning of SoD~
////// by subtledoctor
////////////////////////////////////////////////////////////////////////

//---------------------------------
BEGIN @1013 /* ~Set Starting XP to 65,000~ */ DESIGNATED 8
LABEL ~JasteySodTweaks-AdjustStartingXP~
SUBCOMPONENT @1012 /* ~Change SoD Starting XP~ */
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */

LAF starting_xp INT_VAR value = 65000 END

//---------------------------------
BEGIN @1014 /* ~Set Starting XP to 90,000~ */ DESIGNATED 9
SUBCOMPONENT @1012 /* ~Change SoD Starting XP~ */
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */

LAF starting_xp INT_VAR value = 90000 END

//---------------------------------
BEGIN @1015 /* ~Set Starting XP to 125,000~ */ DESIGNATED 10
SUBCOMPONENT @1012 /* ~Change SoD Starting XP~ */
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */

LAF starting_xp INT_VAR value = 125000 END

//---------------------------------
BEGIN @1016 /* ~Set Starting XP to 161,000~ */ DESIGNATED 11
SUBCOMPONENT @1012 /* ~Change SoD Starting XP~ */
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */

LAF starting_xp INT_VAR value = 161000 END


////////////////////////////////////////////////////////////////////////
//////
////// 8 ~SoD Ending: jastey's Tweaks~
////// 
////////////////////////////////////////////////////////////////////////

BEGIN @1019 /* ~Revised Full Version~ */ DESIGNATED 12
LABEL ~JasteySodTweaks-SoDEnding_RevisedFull~
SUBCOMPONENT @1018  /* ~SoD Ending: jastey's Tweaks~ */
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~c#anotherfinehell.tp2~ ~0~ @1023 /* ~Other optional ending is already installed.~ */

/*
ADD_JOURNAL EXISTING TITLE (%eet_2%59626) @33 USING ~c#sodtweaks/translations/autotra/%LANGUAGE%/sod_revisedend.tra~
*/
<<<<<<<< .../inlined/journal_33.tph
>>>>>>>>
OUTER_SET strref_journal = eet_200000 + 59626
APPEND_OUTER - ~.../inlined/journal_33.tph~ "ADD_JOURNAL EXISTING TITLE (#%strref_journal%) @33 USING ~c#sodtweaks/translations/autotra/%LANGUAGE%/sod_revisedend.tra~"
INCLUDE ~.../inlined/journal_33.tph~

EXTEND_BOTTOM ~bd4100.bcs~   ~c#sodtweaks/revised_end_sod/add_bd4100.baf~
EVALUATE_BUFFER
EXTEND_BOTTOM ~bdchains.bcs~   ~c#sodtweaks/revised_end_sod/add_bdchains.baf~
EVALUATE_BUFFER
EXTEND_TOP ~bdstone.bcs~   ~c#sodtweaks/revised_end_sod/add_bdstone.baf~
EVALUATE_BUFFER

COPY ~c#sodtweaks/revised_end_sod/c#ablank.wav~ ~override~

COMPILE EVALUATE_BUFFER ~c#sodtweaks/revised_end_sod/sod_revisedend.d~

COPY_EXISTING ~bdcut61a.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(PlaySound("AMBDLBOO")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~PlaySound("AMBDLCHE")~
		END
		SPRINT textToReplace ~\(SetGlobal("BD_STOP_OT"\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#st_BD_STOP_OT"~
		END
	END
BUT_ONLY_IF_IT_CHANGES

/* remove boo sounds from bdcut62.bcs */
COPY_EXISTING ~bdcut62.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN

		SPRINT textToReplace ~\(PlaySound("AMBDLBOO")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
		END
	END
BUT_ONLY_IF_IT_CHANGES

INCLUDE ~c#sodtweaks/revised_end_sod/changes_bd6000.tpa~

///////////////////////////
BEGIN @1020 /* ~Revised Streamlined Version~ */ DESIGNATED 13
LABEL ~JasteySodTweaks-SoDEnding_RevisedStreamlined~
SUBCOMPONENT @1018  /* ~SoD Ending: jastey's Tweaks~ */
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~c#anotherfinehell.tp2~ ~0~ @1023 /* ~Other optional ending is already installed.~ */

/*
ADD_JOURNAL EXISTING TITLE (%eet_2%59626) @33 USING ~c#sodtweaks/translations/autotra/%LANGUAGE%/sod_revisedend.tra~
*/
<<<<<<<< .../inlined/journal_33.tph
>>>>>>>>
OUTER_SET strref_journal = eet_200000 + 59626
APPEND_OUTER - ~.../inlined/journal_33.tph~ "ADD_JOURNAL EXISTING TITLE (#%strref_journal%) @33 USING ~c#sodtweaks/translations/autotra/%LANGUAGE%/sod_revisedend.tra~"
INCLUDE ~.../inlined/journal_33.tph~

COPY ~c#sodtweaks/revised_end_sod/c#ablank.wav~ ~override~

/* shorten the revised scene after Skie's death (takes out intermediate cutscene where Corwin and others appear */
COMPILE EVALUATE_BUFFER ~c#sodtweaks/revised_end_sod/sod_revisedend_streamlined_add.d~
~c#sodtweaks/revised_end_sod/sod_revisedend.d~

INCLUDE ~c#sodtweaks/revised_end_sod/streamline_changes.tpa~

INCLUDE ~c#sodtweaks/revised_end_sod/changes_bd6000.tpa~


BEGIN @1022 /* ~Original Streamlined Version~ */ DESIGNATED 14
LABEL ~JasteySodTweaks-SoDEnding_OriginalStreamlined~
SUBCOMPONENT @1018  /* ~SoD Ending: jastey's Tweaks~ */
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~c#anotherfinehell.tp2~ ~0~ @1023 /* ~Other optional ending is already installed.~ */

COMPILE EVALUATE_BUFFER ~c#sodtweaks/revised_end_sod/sod_original_streamlined.d~

INCLUDE ~c#sodtweaks/revised_end_sod/streamline_changes.tpa~


BEGIN @1021 /* ~Skipit Version~ */ DESIGNATED 15
LABEL ~JasteySodTweaks-SoDEnding_Skipit~
SUBCOMPONENT @1018  /* ~SoD Ending: jastey's Tweaks~ */
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~c#anotherfinehell.tp2~ ~0~ @1023 /* ~Other optional ending is already installed.~ */

COMPILE EVALUATE_BUFFER ~c#sodtweaks/revised_end_sod/c#stbd62.baf~

COMPILE EVALUATE_BUFFER ~c#sodtweaks/revised_end_sod/sod_skipitend.d~

COPY_EXISTING ~bdcut61.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(ActionOverride(Player2,LeaveAreaLUA("bd0112","",\[855\.370\],S))[%newline%]*ActionOverride(Player3,LeaveAreaLUA("bd0112","",\[950\.375\],S))[%newline%]*ActionOverride(Player4,LeaveAreaLUA("bd0112","",\[920\.305\],S))[%newline%]*ActionOverride(Player5,LeaveAreaLUA("bd0112","",\[1035\.315\],S))[%newline%]*ActionOverride(Player6,LeaveAreaLUA("bd0112","",\[1000\.255\],S))[%newline%]*LeaveAreaLUAPanic("bd0112","",\[984\.414\],SW)[%newline%]*LeaveAreaLUA("bd0112","",\[984\.414\],SW)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~CreateCreatureImpassable("bdimoitm",[735.235],S)
		ActionOverride("bdimoitm",TakeCreatureItems(Player1,WEAPONS))
		Wait(1)~
		END
		SPRINT textToReplace ~\(EndCutSceneMode()\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~StartCutScene("c#stbd62")~
		END
	END
BUT_ONLY_IF_IT_CHANGES

/* travel trigger directly to sewers exit in bd0104.are */

COPY_EXISTING ~bd0104.are~ ~override~
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel
    fj_box_left     = 1131  //kleinstes x
    fj_box_top      = 759  //kleinstes y
    fj_box_right    = 1198  //größtes x
    fj_box_bottom   = 809   //größtes y
    fj_cursor_idx   = 28   
    fj_vertex_0     = 1131 + (790 << 16)   //kleinstes x +  kleinstes y
    fj_vertex_1     = 1170 + (809 << 16)   //größtes x + kleinstes y
    fj_vertex_2     = 1198 + (779 << 16)   //größtes x + größtes y
    fj_vertex_3     = 1159 + (759 << 16)   //kleinstes x + größtes y
    STR_VAR
    fj_structure_type   = region
    fj_name             = C#Exbd6200
    fj_destination_area = EVAL ~bd6200~
    fj_destination_name = EVAL ~Exitbd6000~
  END

////////////////////////////////////////////////////////////////////////
//////
////// 9 ~Make Portal Close from Dragonspear Castle (Aun Argent survives always)~
////// 
////////////////////////////////////////////////////////////////////////
/* another idea for a tweak: make it so the blood of Belhifet has to be applied from Toril's side. Makes more sense and makes it also that Aun can be rescued even if Caelar died. */

BEGIN @1026 /* ~Make Portal Close from Dragonspear Castle (Aun Argent survives always)~ */ DESIGNATED 20
LABEL ~JasteySoDTweaks_PortalCloseToril~
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @1017
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @1017

COMPILE EVALUATE_BUFFER ~c#sodtweaks/sodportal/c#stc59c.baf~
COMPILE EVALUATE_BUFFER ~c#sodtweaks/sodportal/close_portal_from_toril.d~ USING ~c#sodtweaks/translations/autotra/%LANGUAGE%/portal_sod.tra~

/* add animation of portal closing to bd4300.are */

COPY_EXISTING bd4300.are override
  LPF fj_are_structure
    INT_VAR
    fj_loc_x       = 370
    fj_loc_y       = 290
    fj_flags       = BIT6 | BIT8 | BIT12 | BIT13
    //not covered by wall, draw as background, shown in combat, EE: use wbm
    STR_VAR
    fj_structure_type = animation
    fj_name           = Portal_Closing
    fj_bam_resref     = bd4400pc
    RET
    fj_return_offset
  END
  WRITE_SHORT fj_return_offset + 0x48 512 //width
  WRITE_SHORT fj_return_offset + 0x4a 256 //height
BUT_ONLY

/* change journal entry */
/* #59853 ~The Siege of Dragonspear

Caelar has sacrificed herself to close the portal to Avernus. 

Caelar Argent has remained behind to seal the portal. I was unable to stop her, and in truth, she seemed happy to lay down her life for the safety of the land.~
*/
<<<<<<<< .../inlined/string_set_portaltoril.tph
>>>>>>>>
OUTER_SET strref_59853 = eet_200000 + 59853
APPEND_OUTER - ~.../inlined/string_set_portaltoril.tph~ 
"STRING_SET %strref_59853% @121"
INCLUDE ~.../inlined/string_set_portaltoril.tph~


COPY_EXISTING ~bdcut59.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(IF[%newline%]*!Dead("bdcaelar")[%newline%]*THEN[%newline%]*RESPONSE[%newline%]*#100[%newline%]*CutSceneId("bdcutid2")[%newline%]*ActionOverride("bdaun",MoveToPoint(\[1337\.1213\]))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")
		ActionOverride("bdaun",MoveToPoint([1337.1213]))~
		END
		SPRINT textToReplace ~\(IF[%newline%]*!Dead("bdcaelar")[%newline%]*THEN[%newline%]*RESPONSE[%newline%]*#100[%newline%]*CutSceneId("bdcutid2")[%newline%]*SmallWait(10)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~IF
	False()

THEN
	RESPONSE #100
		CutSceneId("bdcutid2")
		SmallWait(10)~
		END
		SPRINT textToReplace ~\(IF[%newline%]*Dead("bdcaelar")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~IF
	False()~
		END
	END
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~bdcut59b.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(!Dead("bdcaelar")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~True()~
		END
		SPRINT textToReplace ~\(TriggerActivation("portal",FALSE)[%newline%]*AmbientActivate("portal_visuals",FALSE)[%newline%]*AmbientActivate("portal_webm",FALSE)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
		END
		SPRINT textToReplace ~\(EndCutSceneMode()\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~StartCutSceneEx("c#stc59c",TRUE)~
		END
		SPRINT textToReplace ~\(CloseDoor("Door04")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
		END
	END
BUT_ONLY_IF_IT_CHANGES

/* Crossmod: if Another Fine Hell is installed, remove the closing of "Door04" (i.e. the portal animation) */

ACTION_IF MOD_IS_INSTALLED ~c#anotherfinehell.tp2~ ~1~ THEN BEGIN
  COPY_EXISTING ~c#stc59c.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(CloseDoor("Door04")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
		END
    END
  BUT_ONLY_IF_IT_CHANGES
END

////////////////////////////////////////////////////////////////////////
//////
////// 10 ~Hephernaan takes Dragonspear Vault Key~
////// 
////////////////////////////////////////////////////////////////////////
/* New component: Hephernaan would take the vault key from Caelar with him. De Lancie wouldn't be the one responsible to lock the door, but would stand outside with no means to help the trapped PC in a timely fashion.
*/

BEGIN @1025 /* ~Hephernaan takes Dragonspear Vault Key~ */ DESIGNATED 21
LABEL ~JasteySoDTweaks_HephernaanTakesKey~
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~))  @1000 /* ~This component is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @1017
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @1017

COMPILE EVALUATE_BUFFER ~c#sodtweaks/sodportal/hephernaan_takes_key.d~ USING ~c#sodtweaks/translations/autotra/%LANGUAGE%/portal_sod.tra~

/* Change line of Caelar about key */
/* 
#39315 /* ~Here, take this key. It will open the Dragonspear vault. I'll not be returning to Toril.~ [BD39315] */
*/
/* change line about key on Caelar's body in Aun's dialogue */
/* 
#39298 /* ~This is where we part ways, friend. Here, take this key I retrieved from Caelar's body. I know not what it opens, but I'm certain it's nothing in this infernal place.~ */
*/
/* change info line at portal bd4400 */
/*
DisplayString(Myself,62441)  // The portal glows with infernal light. It leads back to Dragonspear, but not to safety, not while Torsin de Lancie holds the portal vault's key. To survive, you must find a way to seal this rift between planes.
*/
<<<<<<<< .../inlined/string_set_caelarkey.tph
>>>>>>>>
OUTER_SET strref_39315 = eet_200000 + 39315
OUTER_SET strref_39298 = eet_200000 + 39298
OUTER_SET strref_62441 = eet_200000 + 62441
APPEND_OUTER - ~.../inlined/string_set_caelarkey.tph~ 
"STRING_SET %strref_39315% @122
STRING_SET %strref_39298% @123
STRING_SET %strref_62441% @124"
INCLUDE ~.../inlined/string_set_caelarkey.tph~





